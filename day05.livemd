# 2022 - Day 05 -- Supply Stacks

```elixir
Mix.install([
  {:kino_aoc, git: "https://github.com/ljgago/kino_aoc"},
  {:kino, "~> 0.7.0"},
  {:kino_vega_lite, "~> 0.1.4"}
])

alias VegaLite, as: Vl
ExUnit.start()
```

## Part 1

The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked crates, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a giant cargo crane capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her which crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates and the rearrangement procedure (your puzzle input). For example:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
```

In this example, there are three stacks of crates. Stack 1 contains two crates: crate Z is on the bottom, and crate N is on top. Stack 2 contains three crates; from bottom to top, they are crates M, C, and D. Finally, stack 3 contains a single crate, P.

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

In the second step, three crates are moved from stack 1 to stack 3. Crates are moved one at a time, so the first crate to be moved (D) ends up below the second and third crates:

```
        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
```

Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved one at a time, crate C ends up below crate M:

```
        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3
```

Finally, one crate is moved from stack 1 to stack 2:

```
        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3
```

The Elves just need to know which crate will end up on top of each stack; in this example, the top crates are C in stack 1, M in stack 2, and Z in stack 3, so you should combine these together and give the Elves the message CMZ.

After the rearrangement procedure completes, what crate ends up on top of each stack?

## Part 2

As you watch the crane operator expertly rearrange the crates, you notice the process isn't following your prediction.

Some mud was covering the writing on the side of the crane, and you quickly wipe it away. The crane isn't a CrateMover 9000 - it's a CrateMover 9001.

The CrateMover 9001 is notable for many new and exciting features: air conditioning, leather seats, an extra cup holder, and the ability to pick up and move multiple crates at once.

Again considering the example above, the crates begin in the same configuration:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

Moving a single crate from stack 2 to stack 1 behaves the same as before:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

However, the action of moving three crates from stack 1 to stack 3 means that those three moved crates stay in the same order, resulting in this new configuration:

```
        [D]
        [N]
    [C] [Z]
    [M] [P]
 1   2   3
```

Next, as both crates are moved from stack 2 to stack 1, they retain their order as well:

```
        [D]
        [N]
[C]     [Z]
[M]     [P]
 1   2   3
```

Finally, a single crate is still moved from stack 1 to stack 2, but now it's crate C that gets moved:

```
        [D]
        [N]
        [Z]
[M] [C] [P]
 1   2   3
```

In this example, the CrateMover 9001 has put the crates in a totally different order: MCD.

Before the rearrangement process finishes, update your simulation so that the Elves know where they should stand to be ready to unload the final supplies. After the rearrangement procedure completes, what crate ends up on top of each stack?

## Solution

```elixir
{:ok, puzzle_input} = KinoAOC.download_puzzle("2022", "5", System.fetch_env!("LB_AOC_COOKIE"))
```

```elixir
example_input = ~s(
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
)
```

```elixir
defmodule DayFive do
  def part_one(input) do
    %{stacks: stacks, moves: moves} = parse(input)
    new_stacks = Enum.reduce(moves, stacks, &move9000(&1, &2))

    Enum.sort(new_stacks, fn {i, _}, {j, _} -> i < j end)
    |> Enum.map(fn
      {_, [top | _]} -> top
      _ -> ?_
    end)
    |> to_string()
  end

  def part_two(input) do
    %{stacks: stacks, moves: moves} = parse(input)
    new_stacks = Enum.reduce(moves, stacks, &move9001(&1, &2))

    Enum.sort(new_stacks, fn {i, _}, {j, _} -> i < j end)
    |> Enum.map(fn
      {_, [top | _]} -> top
      _ -> ?_
    end)
    |> to_string()
  end

  def parse(input) do
    input
    |> String.split("\n\n", trim: true)
    |> then(fn [stacks, moves] ->
      {String.split(stacks, "\n", trim: true) |> Enum.reverse() |> Enum.drop(1),
       String.split(moves, "\n", trim: true)}
    end)
    |> then(fn {stack_lines, moves} ->
      %{stacks: parse_stack(stack_lines), moves: parse_moves(moves)}
    end)
  end

  def parse_stack(lines) do
    lines
    |> Enum.map(&to_charlist(&1))
    |> Enum.reverse()
    |> Enum.map(fn line -> line |> Enum.drop(1) |> Enum.take_every(4) end)
    |> Enum.zip()
    |> Enum.map(fn t ->
      Tuple.to_list(t)
      |> Enum.filter(&Regex.match?(~r/[A-Z]/, to_string([&1])))
    end)
    |> Enum.with_index(1)
    |> Enum.reduce(%{}, fn {charlist, idx}, map -> Map.put(map, idx, charlist) end)
  end

  def parse_moves(moves) do
    moves
    |> Enum.map(fn move ->
      Regex.named_captures(~r/move (?<count>\d+) from (?<from>\d+) to (?<to>\d+)/, move)
    end)
  end

  defp move9000(%{"count" => count, "from" => from, "to" => to}, stacks) do
    count = String.to_integer(count)
    from = String.to_integer(from)
    to = String.to_integer(to)

    Enum.reduce(1..count, stacks, fn _, stacks ->
      [crate | tail] = stacks[from]
      to_stack = stacks[to]
      updated_stacks = %{stacks | from => tail, to => [crate | to_stack]}
      updated_stacks
    end)
  end

  defp move9001(%{"count" => count, "from" => from, "to" => to}, stacks) do
    count = String.to_integer(count)
    from = String.to_integer(from)
    to = String.to_integer(to)

    # Enum.reduce(1..count, stacks, fn _, stacks ->
    {crates, tail} = Enum.split(stacks[from], count)
    to_stack = stacks[to]

    %{
      stacks
      | from => tail,
        to => List.flatten([crates, to_stack])
    }
  end
end
```

```elixir
DayFive.parse(example_input)
```

## Test

```elixir
defmodule Test do
  use ExUnit.Case, async: true
  @example_input ~s(
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
)

  test "part 1" do
    assert DayFive.part_one(@example_input) == "CMZ"
  end

  test "part 2" do
    assert DayFive.part_two(@example_input) == "MCD"
  end
end

ExUnit.run()
```

```elixir
DayFive.part_one(puzzle_input)
```

```elixir
DayFive.part_two(puzzle_input)
```
